.globl write_matrix

.text
# ==============================================================================
# FUNCTION: Writes a matrix of integers into a binary file
# FILE FORMAT:
#   The first 8 bytes of the file will be two 4 byte ints representing the
#   numbers of rows and columns respectively. Every 4 bytes thereafter is an
#   element of the matrix in row-major order.
# Arguments:
#   a0 (char*) is the pointer to string representing the filename
#   a1 (int*)  is the pointer to the start of the matrix in memory
#   a2 (int)   is the number of rows in the matrix
#   a3 (int)   is the number of columns in the matrix
# Returns:
#   None
# Exceptions:
#   - If you receive an fopen error or eof,
#     this function terminates the program with error code 27
#   - If you receive an fclose error or eof,
#     this function terminates the program with error code 28
#   - If you receive an fwrite error or eof,
#     this function terminates the program with error code 30
# ==============================================================================
write_matrix:

    # Prologue
    addi sp, sp, -20
    sw ra, 0(sp)
    sw s0, 4(sp)
    sw s1, 8(sp)
    sw s2 ,12(sp)
    sw s3, 16(sp)
    
    #s0 to store file descriptor
    mv s1, a1 #pointer to matrix
    mv s2, a2 #num of rows
    mv s3, a3 #num of cols

    #open file
    addi a1, x0, 1 #write permission
    jal ra, fopen
    mv s0, a0
    blt a0, x0, openError


    #write number
    #store value into buffer
    addi sp, sp, -8
    sw s2, 0(sp)
    sw s3, 4(sp)

    mv a0, s0
    mv a1, sp
    addi a2, x0, 2
    addi a3, x0, 4
    jal ra, fwrite
    addi sp, sp, 8

    
    addi a2, x0, 2
    bne a0, a2, writeError

    #write later contents
    mv a0, s0
    mul a2, s2, s3 #store number of elements
    addi a3, x0, 4 #bytes per element
    
    #store value into buffer
    mv a1, s1
    jal ra, fwrite
    mul a2, s2, s3
    bne a0, a2, writeError

    #close the file
    mv a0, s0
    jal ra, fclose
    bne a0, x0, closeError

    # Epilogue
    lw ra, 0(sp)
    lw s0, 4(sp)
    lw s1, 8(sp)
    lw s2 ,12(sp)
    lw s3, 16(sp)
    addi sp, sp, 20
    jr ra


openError:
 li a0, 27
 j exit

writeError:
 li a0, 30
 j exit

closeError:
 li a0, 28
 j exit